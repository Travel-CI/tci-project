buildscript {

	ext {
		projectGroupName 	= 'com.travelci'
		projectVersion 		= '0.2-SNAPSHOT'
		projectName			= 'Travel CI'
		sonarqubeUrl		= 'http://esgi.cloudapp.net:9092'

		springBootVersion 	= '1.5.2.RELEASE'
		springCloudVersion 	= 'Dalston.RELEASE'

		jgitVersion			= '4.6.1.201703071140-r'
		dockerClientVersion = '8.2.1'

		junitVersion 		= '4.12'
		restAssuredVersion 	= '2.9.0'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
	}
}

plugins {
	id "org.sonarqube" version "2.4"
}

allprojects {
	group projectGroupName
	version projectVersion
}

sonarqube {
	properties {
		property "sonar.projectName", projectName
		property "sonar.projectKey", projectGroupName
		property "sonar.projectVersion", projectVersion
		property "sonar.sources", "**/src/main/java"
		property "sonar.tests", "**/src/test/java"
		property "sonar.host.url", sonarqubeUrl
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'jacoco'

	jacocoTestReport {
		reports {
			xml.enabled true
			csv.enabled false
		}
	}

	jacocoTestReport.dependsOn test

	repositories {
		maven {
			url 'https://repo.spring.io/libs-snapshot'
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	dependencies {
		compile 'org.springframework.cloud:spring-cloud-starter-config'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
		compile 'org.springframework.boot:spring-boot-starter-actuator'

		compileOnly 'org.projectlombok:lombok'

		testCompile "junit:junit:${junitVersion}"
		testCompile "com.jayway.restassured:rest-assured:${restAssuredVersion}"
		testCompile 'org.springframework.boot:spring-boot-starter-test'
	}
}